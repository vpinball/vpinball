////GLOBAL
#version 400

////DEFINES
#include "Helpers.fxh"

uniform mat4 matWorldViewProj[N_EYES];

////VERTEX
layout(location=0) in vec3 vPosition;
layout(location=1) in vec3 vNormal;
layout(location=2) in vec2 tc;

uniform vec4 clip_plane; // Clip plane defined in table coordinate system
uniform int layer; // Layer being rendered (set to 0 is using layered rendering, that is to say rendering multiple layers using instanced rendering)

VS_OUT(vec3, tablePos);
VS_OUT_EYE;


////GEOMETRY
layout(triangles) in;
layout(triangle_strip, max_vertices = 3) out;

in vec3 tablePos_gs[];
out vec3 tablePos;

in int eye_gs[];
#ifdef SHADER_GL410
flat out int glLayer_gs;
#endif

////FRAGMENT
uniform vec4 lightColor_intensity;
uniform vec4 lightColor2_falloff_power;
uniform vec4 lightCenter_maxRange;
uniform float blend_modulate_vs_add;

in vec3 tablePos;

out vec4 color;
#ifdef SHADER_GL410
flat in int glLayer_gs;
#endif

////vs_lightbulb_main
void main()
{
   float4 pos = vec4(vPosition, 1.0);
   VS_EYE;
   VS_POSITION(matWorldViewProj[eye_vs] * pos, pos);
   VS_VARYING(tablePos, vPosition);
   VS_CLIP_DISTANCE(pos);
}


////gs_lightbulb_main
void main()
{
    GS_LAYER_COPY;
    for (int i=0;i<gl_in.length();i++) {
        gl_Position = matWorldViewProj[eye_gs[i]]*gl_in[i].gl_Position;
        //Disabled since this would move backdrop lights behind other backdrop parts (rendered at z = 0), this could be readded with a max at 0, but I don't get why we would need this type of hack
        //gl_Position.z = max(gl_Position.z, 0.00006103515625); // clamp lights to near clip plane to avoid them being partially clipped // 0.00006103515625 due to 16bit half float
        gl_ClipDistance[0] = gl_in[i].gl_ClipDistance[0];
        tablePos = tablePos_gs[i];
        EmitVertex();
    }
    EndPrimitive();
}


////PS_BulbLight
void main()
{
   FS_LAYER_TO_GLOBAL;
   const float3 light_dir = tablePos - lightCenter_maxRange.xyz;
   const float light_dist = length(light_dir);
   const float len = light_dist * lightCenter_maxRange.w;
   const float atten = pow(max(1.0 - len, 0.0001), lightColor2_falloff_power.w);
   const float3 lcolor = lerp(lightColor2_falloff_power.xyz, lightColor_intensity.xyz, sqrt(len));
   color.rgb = lcolor * (-blend_modulate_vs_add * atten * lightColor_intensity.w); // negative as it will be blended with '1.0-thisvalue' (the 1.0 is needed to modulate the underlying elements correctly, but not wanted for the term below)
   color.a = 1.0 / blend_modulate_vs_add - 1.0; //saturate(atten*lightColor_intensity.w);
}


////PS_BulbLight_with_ball_shadows
#include "BallShadows.fxh"
void main()
{
   FS_LAYER_TO_GLOBAL;
   const float3 light_dir = tablePos - lightCenter_maxRange.xyz;
   const float light_dist = length(light_dir);
   const float len = light_dist * lightCenter_maxRange.w;
   const float atten = pow(max(1.0 - len, 0.0001), lightColor2_falloff_power.w);
   const float3 lcolor = lerp(lightColor2_falloff_power.xyz, lightColor_intensity.xyz, sqrt(len));
   const float shadow = get_light_ball_shadow(lightCenter_maxRange.xyz, light_dir, light_dist);
   color.rgb = lcolor * (-blend_modulate_vs_add * atten * lightColor_intensity.w * shadow); // negative as it will be blended with '1.0-thisvalue' (the 1.0 is needed to modulate the underlying elements correctly, but not wanted for the term below)
   color.a = 1.0 / blend_modulate_vs_add - 1.0; //saturate(atten*lightColor_intensity.w);
}


////TECHNIQUES

bulb_light:P0:vs_lightbulb_main():gs_lightbulb_main():PS_BulbLight()
bulb_light_with_ball_shadows:P0:vs_lightbulb_main():gs_lightbulb_main():PS_BulbLight_with_ball_shadows()

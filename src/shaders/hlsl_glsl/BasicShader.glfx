////GLOBAL
#version 430

////DEFINES
#include "Helpers.fxh"

//!! have switch to choose if texture is weighted by diffuse/glossy or is just used raw?

#define NUM_BALL_LIGHTS 0 // just to avoid having too much constant mem allocated

layout(std140) uniform basicMatrixBlock {
	mat4 matWorld;
	mat4 matView;
	mat4 matWorldView;
	mat3x4 matWorldViewInverseTranspose;
	vec4 unusedFillerFormatWorldViewInverseTranspose;
	mat4 matWorldViewProj[N_EYES];
};

////VERTEX
layout(location=0) in vec3 vPosition;

uniform vec4 clip_plane; // Clip plane defined in table coordinate system
uniform int layer; // Layer being rendered (set to 0 is using layered rendering, that is to say rendering multiple layers using instanced rendering)

VS_OUT_EYE;


////GEOMETRY
layout(triangles) in;
layout(triangle_strip, max_vertices = 3) out;

in int eye_gs[];
#ifdef SHADER_GL410
flat out int glLayer_gs;
#endif

////FRAGMENT
// Early-Z-test implementation:
// Don't enable early_fragment_tests in basicshader, only enable conservative_depth
// Otherwise shader will cull unintended things like mini-playfield under regular playfield
//#extension GL_ARB_shader_image_load_store : enable
#extension GL_ARB_conservative_depth : enable
//layout(early_fragment_tests) in;
#if !defined(SHADER_GL410) && !defined(SHADER_GLES30)
layout(depth_unchanged) out float gl_FragDepth;
#endif

uniform sampler2D tex_base_color; // base texture
uniform sampler2D tex_env; // envmap
uniform sampler2D tex_diffuse_env; // envmap radiance
uniform samplerStereo tex_base_transmission; // bulb light buffer
uniform sampler2D tex_base_normalmap; // normal map
uniform samplerStereo tex_reflection; // reflections
uniform samplerStereo tex_refraction; // refractions
uniform samplerStereo tex_probe_depth; // depth probe

uniform bool objectSpaceNormalMap;

uniform bool is_metal;
uniform bool doNormalMapping;
uniform bool doRefractions;

uniform mat4 matProj[N_EYES]; // Separated from matrix block since we only need to set it once per frame

uniform float4 refractionTint_thickness;
#define refractionTint (refractionTint_thickness.rgb)
#define refractionThickness (refractionTint_thickness.w)

#include "Material_GL.fxh"

uniform float4 cClearcoat_EdgeAlpha;
uniform float4 cGlossy_ImageLerp;
//!! No value is under 0.02
//!! Non-metals value are un-intuitively low: 0.02-0.08
//!! Gemstones are 0.05-0.17
//!! Metals have high specular reflectance: 0.5-1.0

uniform float4 staticColor_Alpha;
uniform float alphaTestValue;

uniform float4 w_h_height;

uniform float4 mirrorNormal_factor;
#define mirrorNormal (mirrorNormal_factor.xyz)
#define mirrorFactor (mirrorNormal_factor.w)
#define doReflections (mirrorNormal_factor.w > 0.0)

out vec4 color;
#ifdef SHADER_GL410
flat in int glLayer_gs;
#endif

uniform float4 lightCenter_doShadow;

#include "BallShadows.fxh"

mat3 TBN_trafo(const float3 N, const float3 V, const float2 uv)
{
   // derivatives: edge vectors for tri-pos and tri-uv
   const float3 dpx = dFdx(V);
   const float3 dpy = dFdy(V);
   const float2 duvx = dFdx(uv);
   const float2 duvy = dFdy(uv);

   // solve linear system
   const float3 dp2perp = cross(N, dpy);
   const float3 dp1perp = cross(dpx, N);
   const float3 T = dp2perp * duvx.x + dp1perp * duvy.x;
   const float3 B = dp2perp * duvx.y + dp1perp * duvy.y;

   // construct scale-invariant transformation
   return mat3(T, B, N * sqrt( max(dot(T,T), dot(B,B)) )); // inverse scale, as will be normalized anyhow later-on (to save some mul's)
}

float3 normal_map(const float3 N, const float3 V, const float2 uv)
{
   const float3 tn = texture(tex_base_normalmap, uv).xyz * (255./127.) - (128./127.);
   BRANCH if (objectSpaceNormalMap)
   { // Object space: this matches the object space, +X +Y +Z, export/baking in Blender with our trafo setup
      return normalize((matWorldViewInverseTranspose * float3(tn.x, tn.y, -tn.z)).xyz);
   }
   else
   { // Tangent space
      return normalize((TBN_trafo(N, V, uv) * tn).xyz);
   }
}

// Compute reflections from reflection probe (screen space coordinates)
// This is a simplified reflection model where reflected light is added instead of being mixed according to the fresnel coefficient (stronger reflection hiding the object's color at grazing angles)
float3 compute_reflection(const float2 screenSpace, const float3 N, const float eye)
{
   // Only apply to faces pointing in the direction of the probe (normal = [0,0,-1])
   // the smoothstep values are *magic* values taken from visual tests
   // dot(mirrorNormal, N) does not really needs to be done per pixel and could be moved to the vertx shader
   // Offset by half a texel to use GPU filtering for some blur
   return smoothstep(0.5, 0.9, dot(mirrorNormal, N)) * mirrorFactor * texStereo(tex_reflection, screenSpace + 0.5 * w_h_height.xy).rgb;
}

// Compute refractions from screen space probe
float3 compute_refraction(const float3 pos, const float2 screenSpace, const float3 N, const float3 V, const float eye)
{
   // Compute refracted visible position then project from world view position to probe UV
   // const float4x4 matProj = mul(inverse4x4(matWorldView), matWorldViewProj[int(eye)]); // this has been moved to the matrix uniform stack for performance reasons
   const float3 R = refract(V, N, 1.0 / 1.5); // n1 = 1.0 (air), n2 = 1.5 (plastic), eta = n1 / n2
   const float3 refracted_pos = pos + refractionThickness * R; // Shift ray by the thickness of the material
   const float4 proj = matProj[int(eye)] * float4(refracted_pos, 1.0);

   float2 uv = float2(0.5, 0.5) + proj.xy * (0.5 / proj.w);

   // Check if the sample position is behind the object pos. If not take don't perform refraction as it would lead to refract things above us (so reflect)
   const float d = texStereo(tex_probe_depth, uv).x;
   if (d < gl_FragCoord.z)
      uv = screenSpace;

   // The following code gives a smoother transition but depends too much on the POV since it uses homogeneous depth to lerp instead of fragment's world depth
   //const float3 unbiased = float3(1.0, 0.0, 0.0);
   //const float3 biased = float3(0.0, 1.0, 0.0);
   //const float3 unbiased = tex2D(tex_refraction, screenSpace * w_h_height.xy).rgb;
   //const float3 biased = tex2D(tex_refraction, uv).rgb;
   //return lerp(unbiased, biased, saturate((d - gl_FragCoord.z) / gl_FragCoord.w));

   /* // Debug output
   if (length(N) < 0.5) // invalid normal, shown as red for debugging
      return float3(1.0, 0.0, 0.0);
   if (dot(N, V) < 0.0) // Wrong orientation? (looking from the back, shown as blue for debugging)
      return float3(0.0, 0.0, 1.0);
   if (length(R) < 0.5) // invalid refraction state (no refraction, shown as green for debugging)
      return float3(0.0, 1.0, 0.0);*/

   return refractionTint.rgb * texStereo(tex_refraction, uv).rgb;
}

//------------------------------------
//
// Standard Materials
//

//-----------------------------------------------------------------------------
// Vertex shaders
//-----------------------------------------------------------------------------

////vs_main

layout(location=1) in vec3 vNormal;
layout(location=2) in vec2 tc;

VS_OUT(vec2, tex01);
VS_OUT(vec3, tablePos);
VS_OUT(vec3, worldPos);
VS_OUT(vec3, normal);

void main()
{
   float4 pos = float4(vPosition, 1.0);
   float3 table_pos = (matWorld * pos).xyz;

   VS_EYE;
   VS_POSITION(matWorldViewProj[eye_vs] * pos, pos);
   VS_VARYING(tablePos, table_pos);
   VS_VARYING(tex01, tc);
   // trafo all into worldview space (as most of the weird trafos happen in view, world is identity so far)
   VS_VARYING(worldPos, (matWorldView * pos).xyz);
   // Needs to use a 'normal' matrix, and to normalize since we allow non uniform stretching, therefore matWorldView is not orthonormal
   VS_VARYING(normal, normalize((matWorldViewInverseTranspose*vNormal).xyz));
   VS_CLIP_DISTANCE(vec4(table_pos.xyz, 1.0));
}

////vs_notex_main

layout(location=1) in vec3 vNormal;
layout(location=2) in vec2 tc;

VS_OUT(vec3, tablePos);
VS_OUT(vec3, worldPos);
VS_OUT(vec3, normal);

void main()
{
   float4 pos = float4(vPosition, 1.0);
   float3 table_pos = (matWorld * pos).xyz;

   VS_EYE;
   VS_POSITION(matWorldViewProj[eye_vs] * pos, pos);
   VS_VARYING(tablePos, table_pos);
   // trafo all into worldview space (as most of the weird trafos happen in view, world is identity so far)
   VS_VARYING(worldPos, (matWorldView * pos).xyz);
   // Needs to use a 'normal' matrix, and to normalize since we allow non uniform stretching, therefore matWorldView is not orthonormal
   VS_VARYING(normal, normalize((matWorldViewInverseTranspose*vNormal).xyz));
   VS_CLIP_DISTANCE(vec4(table_pos.xyz, 1.0));
}

////vs_depth_only_main_without_texture

void main()
{ 
   float4 pos = float4(vPosition, 1.0);
   float3 table_pos = (matWorld * pos).xyz;

   VS_EYE;
   VS_POSITION(matWorldViewProj[eye_vs] * pos, pos);
   VS_CLIP_DISTANCE(vec4(table_pos.xyz, 1.0));
}

////vs_depth_only_main_with_texture

layout(location=2) in vec2 tc;

VS_OUT(vec2, tex0);

void main()
{
   float4 pos = float4(vPosition, 1.0);
   float3 table_pos = (matWorld * pos).xyz;

   VS_EYE;
   VS_POSITION(matWorldViewProj[eye_vs] * pos, pos);
   VS_VARYING(tex0, tc);
   VS_CLIP_DISTANCE(vec4(table_pos.xyz, 1.0));
}

////vs_main_unshaded_without_texture_shadow

VS_OUT(vec3, tablePos);

void main()
{ 
   float4 pos = float4(vPosition, 1.0);
   float3 table_pos = (matWorld * pos).xyz;

   VS_EYE;
   VS_POSITION(matWorldViewProj[eye_vs] * pos, pos);
   VS_VARYING(tablePos, table_pos);
   VS_CLIP_DISTANCE(vec4(table_pos.xyz, 1.0));
}

////vs_main_unshaded_with_texture_shadow

layout(location=2) in vec2 tc;

VS_OUT(vec3, tablePos);
VS_OUT(vec2, tex0);

void main()
{
   float4 pos = float4(vPosition, 1.0);
   float3 table_pos = (matWorld * pos).xyz;

   VS_EYE;
   VS_POSITION(matWorldViewProj[eye_vs] * pos, pos);
   VS_VARYING(tablePos, table_pos);
   VS_VARYING(tex0, tc);
   VS_CLIP_DISTANCE(vec4(table_pos.xyz, 1.0));
}


//-----------------------------------------------------------------------------
// Geometry shaders
//-----------------------------------------------------------------------------


////gs_main

in vec2 tex01_gs[];
out vec2 tex01;

in vec3 worldPos_gs[];
out vec3 worldPos;

in vec3 normal_gs[];
out vec3 normal;

in vec3 tablePos_gs[];
out float3 tablePos;

flat out float eye;

void main()
{
   GS_LAYER_COPY;
   for (int i=0; i<gl_in.length(); i++) {
      gl_Position = matWorldViewProj[eye_gs[i]] * gl_in[i].gl_Position;
      gl_ClipDistance[0] = gl_in[i].gl_ClipDistance[0];
      tex01 = tex01_gs[i];
      tablePos = tablePos_gs[i];
      worldPos = worldPos_gs[i];
      normal = normal_gs[i];
      eye = eye_gs[i];
      EmitVertex();
   }
   EndPrimitive();
}

////gs_notex_main

in vec3 worldPos_gs[];
out vec3 worldPos;

in vec3 normal_gs[];
out vec3 normal;

in vec3 tablePos_gs[];
out float3 tablePos;

flat out float eye;

void main()
{
   GS_LAYER_COPY;
   for (int i=0;i<gl_in.length();i++) {
      gl_Position = matWorldViewProj[eye_gs[i]] * gl_in[i].gl_Position;
      gl_ClipDistance[0] = gl_in[i].gl_ClipDistance[0];
      tablePos = tablePos_gs[i];
      worldPos = worldPos_gs[i];
      normal = normal_gs[i];
      eye = eye_gs[i];
      EmitVertex();
   }
   EndPrimitive();
}

////gs_kicker_main

in vec3 worldPos_gs[];
out vec3 worldPos;

in vec3 normal_gs[];
out vec3 normal;

in vec3 tablePos_gs[];
out float3 tablePos;

flat out float eye;

void main()
{
   GS_LAYER_COPY;
   for (int i=0;i<gl_in.length();i++) {
      float4 P2 = gl_in[i].gl_Position;
      P2.z -= 30.0;
      gl_Position.xyw = (matWorldViewProj[eye_gs[i]] * gl_in[i].gl_Position).xyw;
      gl_Position.z = (matWorldViewProj[eye_gs[i]] * P2).z;
      gl_ClipDistance[0] = gl_in[i].gl_ClipDistance[0];
      tablePos = tablePos_gs[i];
      worldPos = worldPos_gs[i];
      normal = normal_gs[i];
      eye = eye_gs[i];
      EmitVertex();
   }
   EndPrimitive();
}

////gs_depth_only_main_without_texture

void main()
{
   GS_LAYER_COPY;
   for (int i=0;i<gl_in.length();i++) {
      gl_Position = matWorldViewProj[eye_gs[i]] * gl_in[i].gl_Position;
      gl_ClipDistance[0] = gl_in[i].gl_ClipDistance[0];
      EmitVertex();
   }
   EndPrimitive();
}

////gs_depth_only_main_with_texture

in vec2 tex0_gs[];
out vec2 tex0;

void main()
{
   GS_LAYER_COPY;
   for (int i=0;i<gl_in.length();i++) {
      gl_Position = matWorldViewProj[eye_gs[i]] * gl_in[i].gl_Position;
      gl_ClipDistance[0] = gl_in[i].gl_ClipDistance[0];
      tex0 = tex0_gs[i];
      EmitVertex();
   }
   EndPrimitive();
}

////gs_main_unshaded_without_texture_shadow

in vec3 tablePos_gs[];
out float3 tablePos;

void main()
{
   GS_LAYER_COPY;
   for (int i=0;i<gl_in.length();i++) {
      gl_Position = (matWorldViewProj[eye_gs[i]] * gl_in[i].gl_Position);
      gl_ClipDistance[0] = gl_in[i].gl_ClipDistance[0];
      tablePos = tablePos_gs[i];
      EmitVertex();
   }
   EndPrimitive();
}

////gs_main_unshaded_with_texture_shadow

in float3 tablePos_gs[];
out float3 tablePos;

in vec2 tex0_gs[];
out vec2 tex0;

void main()
{
   GS_LAYER_COPY;
   for (int i=0;i<gl_in.length();i++) {
      gl_Position = (matWorldViewProj[eye_gs[i]] * gl_in[i].gl_Position);
      gl_ClipDistance[0] = gl_in[i].gl_ClipDistance[0];
      tablePos = tablePos_gs[i];
      tex0 = tex0_gs[i];
      EmitVertex();
   }
   EndPrimitive();
}



//-----------------------------------------------------------------------------
// Fragment shaders
//-----------------------------------------------------------------------------


////ps_main_notex

in vec3 worldPos;
in vec3 tablePos;
in vec3 normal;
flat in float eye;

void main()
{
   FS_LAYER_TO_GLOBAL;
   const float3 diffuse  = cBase_Alpha.xyz;
   const float3 glossy   = is_metal ? cBase_Alpha.xyz : cGlossy_ImageLerp.xyz*0.08;
   const float3 specular = cClearcoat_EdgeAlpha.xyz*0.08;
   const float  edge     = is_metal ? 1.0 : Roughness_WrapL_Edge_Thickness.z;

   const float3 V = normalize(/*camera=0,0,0,1*/-worldPos); //!! have a "real" view vector instead that mustn't assume that viewer is directly in front of monitor? (e.g. cab setup) -> viewer is always relative to playfield and/or user definable
   const float3 N = normalize(normal);

   color = float4(lightLoop(worldPos, N, V, diffuse, glossy, specular, edge, is_metal), cBase_Alpha.a);

   BRANCH if (color.a < 1.0) // We may not opacify if we already are opaque
   {
      color.a = GeometricOpacity(dot(N,V), color.a, cClearcoat_EdgeAlpha.w, Roughness_WrapL_Edge_Thickness.w);

      if (fDisableLighting_top_below.y < 1.0)
         // add light from "below" from user-flagged bulb lights, pre-rendered/blurred in previous renderpass //!! sqrt = magic
         color.rgb += lerp(sqrt(diffuse)*texStereoNoLod(tex_base_transmission, gl_FragCoord.xy * w_h_height.xy).rgb*color.a, float3(0.), fDisableLighting_top_below.y); //!! depend on normal of light (unknown though) vs geom normal, too?
   }

   BRANCH if (lightCenter_doShadow.w != 0.)
   {
      const float3 light_dir = tablePos.xyz - lightCenter_doShadow.xyz;
      const float light_dist = length(light_dir);
      const float shadow = get_light_ball_shadow(lightCenter_doShadow.xyz, light_dir, light_dist);
      color.rgb *= shadow;
   }

   BRANCH if (doReflections)
#ifndef SHADER_GLES30
      color.rgb += compute_reflection(gl_FragCoord.xy * w_h_height.xy, N, eye);
#else
      color.rgb += compute_reflection(gl_FragCoord.xy * 0.5 + vec2(0.5), N, eye);
#endif

   BRANCH if (doRefractions)
   {
      // alpha channel is the transparency of the object, tinting is supported even if alpha is 0 by applying a tint color
      color.rgb = lerp(compute_refraction(worldPos.xyz, gl_FragCoord.xy * w_h_height.xy, N, V, eye), color.rgb, cBase_Alpha.a);
      color.a = 1.0;
   }

   color *= staticColor_Alpha;
}

////ps_main_texture

in vec2 tex01;
in vec3 worldPos;
in vec3 tablePos;
in vec3 normal;
flat in float eye;

void main()
{
   FS_LAYER_TO_GLOBAL;
   float4 pixel = texture(tex_base_color, tex01);

   pixel.a *= cBase_Alpha.a;
   if (fDisableLighting_top_below.x < 1.0) // if there is lighting applied, make sure to clamp the values (as it could be coming from a HDR tex)
      pixel.xyz = saturate(pixel.xyz);
   const float3 diffuse  = pixel.xyz * cBase_Alpha.xyz;
   const float3 glossy   = is_metal ? diffuse : (pixel.xyz * cGlossy_ImageLerp.w + (1.0-cGlossy_ImageLerp.w)) * cGlossy_ImageLerp.xyz * 0.08; //!! use AO for glossy? specular?
   const float3 specular = cClearcoat_EdgeAlpha.xyz * 0.08;
   const float  edge     = is_metal ? 1.0 : Roughness_WrapL_Edge_Thickness.z;

   const float3 V = normalize(/*camera=0,0,0,1*/-worldPos);
   float3 N = normalize(normal);
   BRANCH if (doNormalMapping)
      N = normal_map(N, V, tex01);

   color = float4(lightLoop(worldPos, N, V, diffuse, glossy, specular, edge, is_metal), pixel.a);

   BRANCH if (color.a < 1.0) // We may not opacify if we already are opaque
   {
      color.a = GeometricOpacity(dot(N,V), color.a, cClearcoat_EdgeAlpha.w, Roughness_WrapL_Edge_Thickness.w);

      BRANCH if (fDisableLighting_top_below.y < 1.0)
         // add light from "below" from user-flagged bulb lights, pre-rendered/blurred in previous renderpass //!! sqrt = magic
         color.rgb += lerp(sqrt(diffuse)*texStereoNoLod(tex_base_transmission, gl_FragCoord.xy * w_h_height.xy).rgb*color.a, float3(0.), fDisableLighting_top_below.y); //!! depend on normal of light (unknown though) vs geom normal, too?
   }

   BRANCH if (lightCenter_doShadow.w != 0.)
   {
      const float3 light_dir = tablePos.xyz - lightCenter_doShadow.xyz;
      const float light_dist = length(light_dir);
      const float shadow = get_light_ball_shadow(lightCenter_doShadow.xyz, light_dir, light_dist);
      color.rgb *= shadow;
   }

   BRANCH if (doReflections)
      color.rgb += compute_reflection(gl_FragCoord.xy * w_h_height.xy, N, eye);

   BRANCH if (doRefractions)
   {
      // alpha channel is the transparency of the object, tinting is supported even if alpha is 0 by applying a tint color (not from main texture since this is different information (reflected/refracted color))
      color.rgb = lerp(compute_refraction(worldPos, gl_FragCoord.xy * w_h_height.xy, N, V, eye), color.rgb, color.a);
      color.a = 1.0;
   }

   // visualize normals for debugging
   // color.rgb = color.rgb * 0.0001 + 0.5 + 0.5 * N.rgb;

   color *= staticColor_Alpha;
}


////ps_main_texture_alphatest
// Copy / Paste from above with alpha testing (which breaks early depth testing) on texture alpha for backward compatibility

in vec2 tex01;
in vec3 worldPos;
in vec3 tablePos;
in vec3 normal;
flat in float eye;

void main() {
   float4 pixel = texture(tex_base_color, tex01);

   if (pixel.a <= alphaTestValue)
      discard; //stop the pixel shader if alpha test should reject pixel. This breaks early depth testing.

   pixel.a *= cBase_Alpha.a;
   if (fDisableLighting_top_below.x < 1.0) // if there is lighting applied, make sure to clamp the values (as it could be coming from a HDR tex)
      pixel.xyz = saturate(pixel.xyz);
   const float3 diffuse  = pixel.xyz * cBase_Alpha.xyz;
   const float3 glossy   = is_metal ? diffuse : (pixel.xyz * cGlossy_ImageLerp.w + (1.0-cGlossy_ImageLerp.w)) * cGlossy_ImageLerp.xyz * 0.08; //!! use AO for glossy? specular?
   const float3 specular = cClearcoat_EdgeAlpha.xyz * 0.08;
   const float  edge     = is_metal ? 1.0 : Roughness_WrapL_Edge_Thickness.z;

   const float3 V = normalize(/*camera=0,0,0,1*/-worldPos);
   float3 N = normalize(normal);
   BRANCH if (doNormalMapping)
      N = normal_map(N, V, tex01);

   color = float4(lightLoop(worldPos, N, V, diffuse, glossy, specular, edge, is_metal), pixel.a);

   BRANCH if (color.a < 1.0) // We may not opacify if we already are opaque
   {
      color.a = GeometricOpacity(dot(N,V), color.a, cClearcoat_EdgeAlpha.w, Roughness_WrapL_Edge_Thickness.w);

      if (fDisableLighting_top_below.y < 1.0)
         // add light from "below" from user-flagged bulb lights, pre-rendered/blurred in previous renderpass //!! sqrt = magic
         color.rgb += lerp(sqrt(diffuse)*texStereoNoLod(tex_base_transmission, gl_FragCoord.xy * w_h_height.xy).rgb*color.a, float3(0.), fDisableLighting_top_below.y); //!! depend on normal of light (unknown though) vs geom normal, too?
   }

   BRANCH if (lightCenter_doShadow.w != 0.)
   {
      const float3 light_dir = tablePos.xyz - lightCenter_doShadow.xyz;
      const float light_dist = length(light_dir);
      const float shadow = get_light_ball_shadow(lightCenter_doShadow.xyz, light_dir, light_dist);
      color.rgb *= shadow;
   }

   BRANCH if (doReflections)
#ifndef SHADER_GLES30
      color.rgb += compute_reflection(gl_FragCoord.xy * w_h_height.xy, N, eye);
#else
      color.rgb += compute_reflection(gl_FragCoord.xy * 0.5 + vec2(0.5), N, eye);
#endif

   BRANCH if (doRefractions)
   {
      // alpha channel is the transparency of the object, tinting is supported even if alpha is 0 by applying a tint color (not from main texture since this is different information (reflected/refracted color))
      color.rgb = lerp(compute_refraction(worldPos, gl_FragCoord.xy * w_h_height.xy, N, V, eye), color.rgb, color.a);
      color.a = 1.0;
   }

   color *= staticColor_Alpha;
}


////ps_main_reflection_only

in vec2 tex01;
in vec3 worldPos;
in vec3 normal;
flat in float eye;

void main()
{
   FS_LAYER_TO_GLOBAL;
   const float3 N = normalize(normal);
#ifndef SHADER_GLES30
   color.rgb = compute_reflection(gl_FragCoord.xy * w_h_height.xy, N, eye);
#else
   color.rgb = compute_reflection(gl_FragCoord.xy * 0.5 + vec2(0.5), N, eye);
#endif
   color.a = 1.0;
   color *= staticColor_Alpha;
}


////ps_main_unshaded_without_texture

void main()
{
   FS_LAYER_TO_GLOBAL;
   color = staticColor_Alpha;
}


////ps_main_unshaded_with_texture

in vec2 tex0;

void main()
{
   FS_LAYER_TO_GLOBAL;
   color = staticColor_Alpha * texture(tex_base_color, tex0);
}


////ps_main_unshaded_without_texture_shadow

in vec3 tablePos;

void main()
{
   FS_LAYER_TO_GLOBAL;
   const float3 light_dir = tablePos.xyz - lightCenter_doShadow.xyz;
   const float light_dist = length(light_dir);
   const float shadow = get_light_ball_shadow(lightCenter_doShadow.xyz, light_dir, light_dist);
   color = shadow * staticColor_Alpha;
}


////ps_main_unshaded_with_texture_shadow

in vec2 tex0;
in vec3 tablePos;

void main()
{
   FS_LAYER_TO_GLOBAL;
   const float3 light_dir = tablePos.xyz - lightCenter_doShadow.xyz;
   const float light_dist = length(light_dir);
   const float shadow = get_light_ball_shadow(lightCenter_doShadow.xyz, light_dir, light_dist);
   color = shadow * staticColor_Alpha * texture(tex_base_color, tex0);
}


////ps_main_bg_decal

// BG-Decal

void main()
{
   FS_LAYER_TO_GLOBAL;
   color = cBase_Alpha;
}

////ps_main_bg_decal_texture

in vec2 tex01;

void main()
{
   FS_LAYER_TO_GLOBAL;
   float4 pixel = texture(tex_base_color, tex01);
   if (pixel.a <= alphaTestValue) discard;
   color = pixel * cBase_Alpha;
}


//------------------------------------------
// Kicker boolean vertex shader

////vs_kicker

in vec3 vNormal;
in vec2 tc;

VS_OUT(float3, tablePos);
VS_OUT(vec3, worldPos);
VS_OUT(vec3, normal);

void main()
{
   float4 pos = float4(vPosition, 1.0);
   float3 table_pos = (matWorld * pos).xyz;

   VS_EYE;
   #if USE_GEOMETRY_SHADER
   gl_Position = pos;
   #else
   gl_Position = matWorldViewProj[eye_vs] * pos;
   gl_Position.z = (matWorldViewProj[eye_vs] * float4(pos.x, pos.y, pos.z - 30.0, 1.0)).z;
   #endif
   VS_VARYING(tablePos, table_pos);
   // trafo all into worldview space (as most of the weird trafos happen in view, world is identity so far)
   VS_VARYING(worldPos, (matWorldView * pos).xyz);
   // Needs to use a 'normal' matrix, and to normalize since we allow non uniform stretching, therefore matWorldView is not orthonormal
   VS_VARYING(normal, normalize((matWorldViewInverseTranspose*vNormal).xyz));
   VS_CLIP_DISTANCE(vec4(table_pos.xyz, 1.0));
}


//------------------------------------
// Techniques
//

//
// Standard Materials
//
////TECHNIQUES

basic_without_texture:P0:vs_notex_main():gs_notex_main():ps_main_notex()
basic_with_texture:P0:vs_main():gs_main():ps_main_texture()
basic_with_texture_at:P0:vs_main():gs_main():ps_main_texture_alphatest()

basic_reflection_only:P0:vs_notex_main():gs_notex_main():ps_main_reflection_only()

unshaded_without_texture:P0:vs_depth_only_main_without_texture():gs_depth_only_main_without_texture():ps_main_unshaded_without_texture()
unshaded_with_texture:P0:vs_depth_only_main_with_texture():gs_depth_only_main_with_texture():ps_main_unshaded_with_texture()
unshaded_without_texture_shadow:P0:vs_main_unshaded_without_texture_shadow():gs_main_unshaded_without_texture_shadow():ps_main_unshaded_without_texture_shadow()
unshaded_with_texture_shadow:P0:vs_main_unshaded_with_texture_shadow():gs_main_unshaded_with_texture_shadow():ps_main_unshaded_with_texture_shadow()

bg_decal_without_texture:P0:vs_notex_main():gs_notex_main():ps_main_bg_decal()
bg_decal_with_texture:P0:vs_main():gs_main():ps_main_bg_decal_texture()

kickerBoolean:P0:vs_kicker():gs_kicker_main():ps_main_notex()

#include "ClassicLightShader.glfx"

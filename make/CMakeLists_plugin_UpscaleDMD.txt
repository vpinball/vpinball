# plugin CMake file to be included in main project CMakelists

# --- UpscaleDMD plugin ---

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_C_STANDARD 99)

set(UPSCALEDMD_PLUGIN_SOURCES
   plugins/upscaledmd/upscaledmd.cpp
   plugins/upscaledmd/xbrz/xbrz.cpp
   plugins/upscaledmd/xbrz/xbrz.h
   plugins/upscaledmd/super-xbr/super-xbr.h
   plugins/upscaledmd/scalefx/scalefx.h
   src/core/ResURIResolver.h
   src/core/ResURIResolver.cpp
)

if(BUILD_SHARED)
   add_library(UpscaleDMDPlugin MODULE ${UPSCALEDMD_PLUGIN_SOURCES})

   target_include_directories(UpscaleDMDPlugin PUBLIC
      ${CMAKE_SOURCE_DIR}/third-party/include
      plugins
      plugins/upscaledmd
      src/core
   )

   if(PluginPlatform STREQUAL "windows")
      add_compile_options(
         $<$<CONFIG:RELEASE>:/Ob2>
         $<$<CONFIG:RELEASE>:/O2>
         $<$<CONFIG:RELEASE>:/Oi>
         $<$<CONFIG:RELEASE>:/arch:SSE2>
         $<$<CONFIG:RELEASE>:/fp:fast>
         $<$<CONFIG:RELEASE>:/fp:except->
         $<$<CONFIG:RELEASE>:/Ot>
         $<$<CONFIG:RELEASE>:/GF>
         $<$<CONFIG:RELEASE>:/GS->
         $<$<CONFIG:RELEASE>:/Gy>
         $<$<CONFIG:RELEASE>:/GR>
         $<$<CONFIG:RELEASE>:/Oy>
         $<$<CONFIG:RELEASE>:/GT>
         $<$<CONFIG:RELEASE>:/GL>
      )
      set_target_properties(UpscaleDMDPlugin PROPERTIES
         MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:DEBUG>:Debug>"
      )
      target_compile_options(UpscaleDMDPlugin PUBLIC
         $<$<CONFIG:RELEASE>:$<$<COMPILE_LANGUAGE:CXX>:${OPT_COMMON}>>
         $<$<CONFIG:RELEASE>:$<$<COMPILE_LANGUAGE:C>:${OPT_COMMON}>>
      )
      target_link_options(UpscaleDMDPlugin PUBLIC
         $<$<CONFIG:RELEASE>:/INCREMENTAL:NO>
         $<$<CONFIG:RELEASE>:/OPT:REF>
         $<$<CONFIG:RELEASE>:/OPT:ICF>
         $<$<CONFIG:RELEASE>:/LTCG>
      )
      if(PluginArch STREQUAL "x64")
         set_target_properties(UpscaleDMDPlugin PROPERTIES
            OUTPUT_NAME "plugin-upscaledmd64"
         )
      else()
         set_target_properties(UpscaleDMDPlugin PROPERTIES
            OUTPUT_NAME "plugin-upscaledmd"
         )
      endif()
   else()
      set_target_properties(UpscaleDMDPlugin PROPERTIES
         PREFIX ""
         OUTPUT_NAME "plugin-upscaledmd"
      )
      if(PluginPlatform STREQUAL "macos")
         set_target_properties(UpscaleDMDPlugin PROPERTIES
            SUFFIX ".dylib"
         )
      endif()
   endif()

   add_custom_command(TARGET UpscaleDMDPlugin POST_BUILD
      COMMAND "${CMAKE_COMMAND}" -E copy "${CMAKE_SOURCE_DIR}/plugins/upscaledmd/plugin.cfg" "${PLUGINS_DIR}/upscaledmd/plugin.cfg"
      COMMAND "${CMAKE_COMMAND}" -E copy "$<TARGET_FILE:UpscaleDMDPlugin>" "${PLUGINS_DIR}/upscaledmd"
      COMMAND "${CMAKE_COMMAND}" -E rm "$<TARGET_FILE:UpscaleDMDPlugin>"
   )
endif()

if(BUILD_STATIC)
   add_compile_options(-fPIC)

   add_library(UpscaleDMDPlugin STATIC ${UPSCALEDMD_PLUGIN_SOURCES})

   target_include_directories(UpscaleDMDPlugin PUBLIC
      ${CMAKE_SOURCE_DIR}/third-party/include
      plugins
      plugins/upscaledmd
      src/core
   )

   set_target_properties(UpscaleDMDPlugin PROPERTIES
      PREFIX ""
      OUTPUT_NAME "plugin-upscaledmd"
   )
endif()

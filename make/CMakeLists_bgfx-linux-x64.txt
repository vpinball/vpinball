cmake_minimum_required(VERSION 3.25)

option(POST_BUILD_COPY_EXT_LIBS "Copy external libraries to build directory" ON)

file(READ src/core/vpversion.h version)
string(REGEX MATCH "VP_VERSION_MAJOR[ ]+([0-9]+)" _tmp ${version})
set(VERSION_MAJOR "${CMAKE_MATCH_1}")
string(REGEX MATCH "VP_VERSION_MINOR[ ]+([0-9]+)" _tmp ${version})
set(VERSION_MINOR "${CMAKE_MATCH_1}")
string(REGEX MATCH "VP_VERSION_REV[ ]+([0-9]+)" _tmp ${version})
set(VERSION_REV "${CMAKE_MATCH_1}")
set(APP_NAME "VPinballX_BGFX")

project(vpinball VERSION "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_REV}")

include("${CMAKE_SOURCE_DIR}/make/CMakeLists_common_standalone.txt")

set(CMAKE_BUILD_WITH_INSTALL_RPATH TRUE)
set(CMAKE_INSTALL_RPATH "$ORIGIN")

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_C_STANDARD 99)

find_package(BISON 3.7.5 REQUIRED)
bison_target(vbsparser ${CMAKE_SOURCE_DIR}/standalone/inc/wine/dlls/vbscript/parser.y ${CMAKE_SOURCE_DIR}/standalone/inc/wine/dlls/vbscript/parser.tab.c 
	     DEFINES_FILE ${CMAKE_SOURCE_DIR}/standalone/inc/wine/dlls/vbscript/parser.tab.h)

add_executable(vpinball
   ${VPX_STANDALONE_SOURCES}

   src/input/OpenPinDevHandler.h
   src/input/OpenPinDevHandler.cpp

   third-party/include/imgui/imgui_impl_sdl3.cpp
   third-party/include/imgui/imgui_impl_sdl3.h
)

target_include_directories(vpinball PUBLIC
   ${CMAKE_SOURCE_DIR}/third-party/include
   ${CMAKE_SOURCE_DIR}
   standalone/inc/wine/include
   standalone/inc/wine/dlls/atl
   standalone/inc/wine/dlls/vbscript
   standalone/inc/wine/dlls/oleaut32
   standalone/inc/wine/dlls/kernelbase
   media
   pin
   src
   src/plugins
)

target_precompile_headers(vpinball PUBLIC
)

target_compile_definitions(vpinball PRIVATE
   __STANDALONE__

   ENABLE_BGFX
   ENABLE_SDL_VIDEO
   ENABLE_SDL_INPUT

   __WINESRC__
   WINE_UNICODE_NATIVE

   WINE_NO_TRACE_MSGS
   WINE_NO_DEBUG_MSGS

   $<$<CONFIG:Debug>:BX_CONFIG_DEBUG=1>
   $<$<CONFIG:Release>:BX_CONFIG_DEBUG=0>

   "__forceinline=__attribute__((always_inline)) inline"
)

target_compile_options(vpinball PUBLIC
   -mssse3
   -fvisibility=hidden
   $<$<OR:$<CXX_COMPILER_ID:Clang>,$<CXX_COMPILER_ID:AppleClang>>:
      -fdenormal-fp-math=preserve-sign
      -freciprocal-math
      -fassociative-math
      -fapprox-func
      -fno-math-errno
      -fno-trapping-math
      -ffp-contract=off
      -Wno-shorten-64-to-32>
   $<$<CXX_COMPILER_ID:GNU>:
      -ffast-math
      -ffp-contract=off>
)

target_link_directories(vpinball PUBLIC
   ${CMAKE_SOURCE_DIR}/third-party/runtime-libs/linux-x64
)

target_link_libraries(vpinball PUBLIC
   bgfx
   SDL3
   SDL3_image
   SDL3_ttf
   freeimage
   hidapi-hidraw
   dof
)

set_target_properties(vpinball PROPERTIES
   RUNTIME_OUTPUT_NAME "${APP_NAME}"
   LINK_FLAGS "-Wl,--copy-dt-needed-entries"
)

add_custom_command(TARGET vpinball POST_BUILD
   COMMAND "${CMAKE_COMMAND}" -E make_directory "$<TARGET_FILE_DIR:vpinball>/user"
   COMMAND "${CMAKE_COMMAND}" -E copy_directory "${CMAKE_SOURCE_DIR}/src/assets" "$<TARGET_FILE_DIR:vpinball>/assets"
   COMMAND "${CMAKE_COMMAND}" -E copy_directory "${CMAKE_SOURCE_DIR}/scripts" "$<TARGET_FILE_DIR:vpinball>/scripts"
   COMMAND "${CMAKE_COMMAND}" -E copy_directory "${CMAKE_SOURCE_DIR}/tables" "$<TARGET_FILE_DIR:vpinball>/tables"
   COMMAND "${CMAKE_COMMAND}" -E copy_directory "${CMAKE_SOURCE_DIR}/docs" "$<TARGET_FILE_DIR:vpinball>/docs"
   COMMAND "${CMAKE_COMMAND}" -E make_directory "$<TARGET_FILE_DIR:vpinball>/shaders-${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_REV}"
)

if(POST_BUILD_COPY_EXT_LIBS)
   add_custom_command(TARGET vpinball POST_BUILD
      COMMAND cp -a "${CMAKE_SOURCE_DIR}/third-party/runtime-libs/linux-x64/libbgfx.so" "$<TARGET_FILE_DIR:vpinball>"
      COMMAND cp -a "${CMAKE_SOURCE_DIR}/third-party/runtime-libs/linux-x64/libSDL3.so" "$<TARGET_FILE_DIR:vpinball>"
      COMMAND cp -a "${CMAKE_SOURCE_DIR}/third-party/runtime-libs/linux-x64/libSDL3.so.*" "$<TARGET_FILE_DIR:vpinball>"
      COMMAND cp -a "${CMAKE_SOURCE_DIR}/third-party/runtime-libs/linux-x64/libSDL3_image.so" "$<TARGET_FILE_DIR:vpinball>"
      COMMAND cp -a "${CMAKE_SOURCE_DIR}/third-party/runtime-libs/linux-x64/libSDL3_image.so.*" "$<TARGET_FILE_DIR:vpinball>"
      COMMAND cp -a "${CMAKE_SOURCE_DIR}/third-party/runtime-libs/linux-x64/libSDL3_ttf.so" "$<TARGET_FILE_DIR:vpinball>"
      COMMAND cp -a "${CMAKE_SOURCE_DIR}/third-party/runtime-libs/linux-x64/libSDL3_ttf.so.*" "$<TARGET_FILE_DIR:vpinball>"
      COMMAND cp -a "${CMAKE_SOURCE_DIR}/third-party/runtime-libs/linux-x64/libfreeimage.so" "$<TARGET_FILE_DIR:vpinball>"
      COMMAND cp -a "${CMAKE_SOURCE_DIR}/third-party/runtime-libs/linux-x64/libfreeimage.so.*" "$<TARGET_FILE_DIR:vpinball>"
      COMMAND cp -a "${CMAKE_SOURCE_DIR}/third-party/runtime-libs/linux-x64/libserialport.so" "$<TARGET_FILE_DIR:vpinball>"
      COMMAND cp -a "${CMAKE_SOURCE_DIR}/third-party/runtime-libs/linux-x64/libserialport.so.*" "$<TARGET_FILE_DIR:vpinball>"
      COMMAND cp -a "${CMAKE_SOURCE_DIR}/third-party/runtime-libs/linux-x64/libhidapi-hidraw.so" "$<TARGET_FILE_DIR:vpinball>"
      COMMAND cp -a "${CMAKE_SOURCE_DIR}/third-party/runtime-libs/linux-x64/libhidapi-hidraw.so.*" "$<TARGET_FILE_DIR:vpinball>"
      COMMAND cp -a "${CMAKE_SOURCE_DIR}/third-party/runtime-libs/linux-x64/libdof.so" "$<TARGET_FILE_DIR:vpinball>"
      COMMAND cp -a "${CMAKE_SOURCE_DIR}/third-party/runtime-libs/linux-x64/libdof.so.*" "$<TARGET_FILE_DIR:vpinball>"
   )
endif()

set(PluginPlatform "linux")
set(PluginArch "x64")
include("${CMAKE_SOURCE_DIR}/make/CMakeLists_plugins.txt")
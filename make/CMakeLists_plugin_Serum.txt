# plugin CMake file to be included in main project CMakelists

# --- Serum colorization plugin ---

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_C_STANDARD 99)

add_library(SerumPlugin MODULE
   plugins/serum/common.cpp
   plugins/serum/serum.cpp
)

target_include_directories(SerumPlugin PUBLIC
   plugins/serum
   ${CMAKE_SOURCE_DIR}/third-party/include
   src/plugins
)

if(PluginPlatform STREQUAL "windows")
   add_compile_options(
      $<$<CONFIG:RELEASE>:/Ob2>
      $<$<CONFIG:RELEASE>:/O2>
      $<$<CONFIG:RELEASE>:/Oi>
      $<$<CONFIG:RELEASE>:/arch:SSE2>
      $<$<CONFIG:RELEASE>:/fp:fast>
      $<$<CONFIG:RELEASE>:/fp:except->
      $<$<CONFIG:RELEASE>:/Ot>
      $<$<CONFIG:RELEASE>:/GF>
      $<$<CONFIG:RELEASE>:/GS->
      $<$<CONFIG:RELEASE>:/Gy>
      $<$<CONFIG:RELEASE>:/GR>
      $<$<CONFIG:RELEASE>:/Oy>
      $<$<CONFIG:RELEASE>:/GT>
      $<$<CONFIG:RELEASE>:/GL>
   )
   set_target_properties(SerumPlugin PROPERTIES
      MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:DEBUG>:Debug>"
   )
   target_compile_options(SerumPlugin PUBLIC
      $<$<CONFIG:RELEASE>:$<$<COMPILE_LANGUAGE:CXX>:${OPT_COMMON}>>
      $<$<CONFIG:RELEASE>:$<$<COMPILE_LANGUAGE:C>:${OPT_COMMON}>>
   )
   target_link_options(SerumPlugin PUBLIC
      $<$<CONFIG:RELEASE>:/INCREMENTAL:NO>
      $<$<CONFIG:RELEASE>:/OPT:REF>
      $<$<CONFIG:RELEASE>:/OPT:ICF>
      $<$<CONFIG:RELEASE>:/LTCG>
   )
   if (PluginArch STREQUAL "x64")
      set_target_properties(SerumPlugin PROPERTIES
         OUTPUT_NAME "plugin-serum64"
      )
      target_link_directories(SerumPlugin PUBLIC ${CMAKE_SOURCE_DIR}/third-party/build-libs/windows-x64)
      target_link_libraries(SerumPlugin serum64.lib)
   else()
      set_target_properties(SerumPlugin PROPERTIES
         OUTPUT_NAME "plugin-serum"
      )
      target_link_directories(SerumPlugin PUBLIC ${CMAKE_SOURCE_DIR}/third-party/build-libs/windows-x86)
      target_link_libraries(SerumPlugin serum.lib)
   endif()
else()
   set_target_properties(SerumPlugin PROPERTIES
      PREFIX ""
      OUTPUT_NAME "plugin-serum"
   )
   target_link_directories(SerumPlugin PUBLIC
      ${CMAKE_SOURCE_DIR}/third-party/runtime-libs/${PluginPlatform}-${PluginArch}
   )
   target_link_libraries(SerumPlugin PUBLIC serum)
endif()

add_custom_command(TARGET SerumPlugin POST_BUILD
   COMMAND "${CMAKE_COMMAND}" -E copy "${CMAKE_SOURCE_DIR}/plugins/serum/plugin.cfg" "${PLUGINS_DIR}/serum/plugin.cfg"
   COMMAND "${CMAKE_COMMAND}" -E copy "$<TARGET_FILE:SerumPlugin>" "${PLUGINS_DIR}/serum"
   COMMAND "${CMAKE_COMMAND}" -E rm "$<TARGET_FILE:SerumPlugin>"
)

if(PluginPlatform STREQUAL "windows")
   if (PluginArch STREQUAL "x64")
      add_custom_command(TARGET SerumPlugin POST_BUILD
         COMMAND "${CMAKE_COMMAND}" -E copy "${CMAKE_SOURCE_DIR}/third-party/runtime-libs/windows-x64/serum64.dll" "${PLUGINS_DIR}/serum/serum64.dll"
      )
   elseif (PluginArch STREQUAL "x86")
      add_custom_command(TARGET SerumPlugin POST_BUILD
         COMMAND "${CMAKE_COMMAND}" -E copy "${CMAKE_SOURCE_DIR}/third-party/runtime-libs/windows-x86/serum.dll" "${PLUGINS_DIR}/serum/serum.dll"
      )
   endif()
endif()

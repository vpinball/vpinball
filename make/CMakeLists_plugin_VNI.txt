# plugin CMake file to be included in main project CMakelists

# --- VNI colorization plugin ---

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_C_STANDARD 99)

set(VNI_PLUGIN_SOURCES
   plugins/vni/common.cpp
   plugins/vni/vni.cpp
)

if(BUILD_SHARED)
   add_library(VNIPlugin MODULE ${VNI_PLUGIN_SOURCES})

   target_include_directories(VNIPlugin PUBLIC
      ${CMAKE_SOURCE_DIR}/third-party/include
      plugins
      plugins/vni
   )

   if(PluginPlatform STREQUAL "windows")
      add_compile_options(
         $<$<CONFIG:RELEASE>:/Ob2>
         $<$<CONFIG:RELEASE>:/O2>
         $<$<CONFIG:RELEASE>:/Oi>
         $<$<CONFIG:RELEASE>:/arch:SSE2>
         $<$<CONFIG:RELEASE>:/fp:fast>
         $<$<CONFIG:RELEASE>:/fp:except->
         $<$<CONFIG:RELEASE>:/Ot>
         $<$<CONFIG:RELEASE>:/GF>
         $<$<CONFIG:RELEASE>:/GS->
         $<$<CONFIG:RELEASE>:/Gy>
         $<$<CONFIG:RELEASE>:/GR>
         $<$<CONFIG:RELEASE>:/Oy>
         $<$<CONFIG:RELEASE>:/GT>
         $<$<CONFIG:RELEASE>:/GL>
      )
      set_target_properties(VNIPlugin PROPERTIES
         MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:DEBUG>:Debug>"
      )
      target_compile_options(VNIPlugin PUBLIC
         $<$<CONFIG:RELEASE>:$<$<COMPILE_LANGUAGE:CXX>:${OPT_COMMON}>>
         $<$<CONFIG:RELEASE>:$<$<COMPILE_LANGUAGE:C>:${OPT_COMMON}>>
      )
      target_link_options(VNIPlugin PUBLIC
         $<$<CONFIG:RELEASE>:/INCREMENTAL:NO>
         $<$<CONFIG:RELEASE>:/OPT:REF>
         $<$<CONFIG:RELEASE>:/OPT:ICF>
         $<$<CONFIG:RELEASE>:/LTCG>
      )
      if (PluginArch STREQUAL "x64")
         set_target_properties(VNIPlugin PROPERTIES
            OUTPUT_NAME "plugin-vni64"
         )
         target_link_directories(VNIPlugin PUBLIC ${CMAKE_SOURCE_DIR}/third-party/build-libs/windows-x64)
         target_link_libraries(VNIPlugin vni64.lib)
      else()
         set_target_properties(VNIPlugin PROPERTIES
            OUTPUT_NAME "plugin-vni"
         )
         target_link_directories(VNIPlugin PUBLIC ${CMAKE_SOURCE_DIR}/third-party/build-libs/windows-x86)
         target_link_libraries(VNIPlugin vni.lib)
      endif()
   else()
      set_target_properties(VNIPlugin PROPERTIES
         PREFIX ""
         OUTPUT_NAME "plugin-vni"
      )
      if(PluginPlatform STREQUAL "macos")
         set_target_properties(VNIPlugin PROPERTIES
            SUFFIX ".dylib"
         )
      endif()
      target_link_directories(VNIPlugin PUBLIC
         ${CMAKE_SOURCE_DIR}/third-party/runtime-libs/${PluginPlatform}-${PluginArch}
      )
      target_link_libraries(VNIPlugin PUBLIC vni)
   endif()

   add_custom_command(TARGET VNIPlugin POST_BUILD
      COMMAND "${CMAKE_COMMAND}" -E copy "${CMAKE_SOURCE_DIR}/plugins/vni/plugin.cfg" "${PLUGINS_DIR}/vni/plugin.cfg"
      COMMAND "${CMAKE_COMMAND}" -E copy "$<TARGET_FILE:VNIPlugin>" "${PLUGINS_DIR}/vni"
      COMMAND "${CMAKE_COMMAND}" -E rm "$<TARGET_FILE:VNIPlugin>"
   )

   if(POST_BUILD_COPY_EXT_LIBS)
      if(PluginPlatform STREQUAL "windows")
         if (PluginArch STREQUAL "x64")
            add_custom_command(TARGET VNIPlugin POST_BUILD
               COMMAND "${CMAKE_COMMAND}" -E copy "${CMAKE_SOURCE_DIR}/third-party/runtime-libs/windows-${PluginArch}/vni64.dll" "${PLUGINS_DIR}/vni/vni64.dll"
            )
         elseif (PluginArch STREQUAL "x86")
            add_custom_command(TARGET VNIPlugin POST_BUILD
               COMMAND "${CMAKE_COMMAND}" -E copy "${CMAKE_SOURCE_DIR}/third-party/runtime-libs/windows-${PluginArch}/vni.dll" "${PLUGINS_DIR}/vni/vni.dll"
            )
         endif()
      elseif(PluginPlatform STREQUAL "linux")
         add_custom_command(TARGET VNIPlugin POST_BUILD
            COMMAND cp -a "${CMAKE_SOURCE_DIR}/third-party/runtime-libs/linux-${PluginArch}/libvni.so" "${PLUGINS_DIR}/vni/"
            COMMAND cp -a "${CMAKE_SOURCE_DIR}/third-party/runtime-libs/linux-${PluginArch}/libvni.so.*" "${PLUGINS_DIR}/vni/"
         )
      endif()
   endif()
endif()

if(BUILD_STATIC)
   add_compile_options(-fPIC)

   add_library(VNIPlugin STATIC ${VNI_PLUGIN_SOURCES})

   target_include_directories(VNIPlugin PUBLIC
      ${CMAKE_SOURCE_DIR}/third-party/include
      plugins
      plugins/vni
   )

   set_target_properties(VNIPlugin PROPERTIES
      PREFIX ""
      OUTPUT_NAME "plugin-vni"
   )
endif()
